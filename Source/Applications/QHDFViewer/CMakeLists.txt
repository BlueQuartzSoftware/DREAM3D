#--////////////////////////////////////////////////////////////////////////////
#--
#--  Copyright (c) 2009, 2010 Michael A. Jackson. BlueQuartz Software
#--  All rights reserved.
#--  BSD License: http://www.opensource.org/licenses/bsd-license.html
#--
#-- This code was partly written under US Air Force Contract FA8650-07-D-5800
#--
#--////////////////////////////////////////////////////////////////////////////
PROJECT (QHDFViewer)
cmake_minimum_required(VERSION 2.8.9)


# ------------------------------------------------------------------------------
#  Qt 4 Section
# ------------------------------------------------------------------------------
if(QT4_FOUND)
    include( ${QT_USE_FILE} )
    #CMP_COPY_QT4_RUNTIME_LIBRARIES( "QtSvg;QtScript")
    #CMP_QT_LIBRARIES_INSTALL_RULES( "QtSvg;QtScript" "./")
else()
    message(FATAL_ERROR "Qt 4 is Needed for this project.")
endif(QT4_FOUND)

SET (QHDFViewer_EXE_NAME QHDFViewer)

# Set the GUI type for different platforms
SET(GUI_TYPE )

# Set some Win32 Specific Settings
if(WIN32)
 SET(GUI_TYPE WIN32)
ENDif(WIN32)

add_definitions(-DDREAM3D_USE_QT)


# --------------------------------------------------------------------
# Generate the License File Headers
set(PROJECT_PREFIX "QHDFViewer")
include(${DREAM3DProj_SOURCE_DIR}/License/License.cmake)


# ---------- Set up the Project Source Files ------------------
# with SET() command you can change variables or define new ones
# here we define QHDFViewer_SRCS variable that contains a list of all .cpp files
# note that we don't need \ at the end of line
SET( QHDFViewer_SRCS
  ${QHDFViewer_SOURCE_DIR}/main.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDFViewer.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDFDataTableWindow.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDFStringDataWindow.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDFImageDataWindow.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDFGroupPropertiesWindow.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDFPropertiesWindow.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDFViewerApplication.cpp
  ${QHDFViewer_SOURCE_DIR}/IH5DataWindow.cpp
  ${QHDFViewer_SOURCE_DIR}/QDockUtilities.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDF5TableModel.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDF5TreeModelItem.cpp
  ${QHDFViewer_SOURCE_DIR}/QHDF5TreeModel.cpp
  ${QHDFViewer_SOURCE_DIR}/HDFGraphicsDelegate.cpp
  ${QHDFViewer_SOURCE_DIR}/QRecentFileList.cpp
  ${QHDFViewer_SOURCE_DIR}/ImageDataToolbar.cpp
  
)

set(QHDFViewer_HDRS
  ${QHDFViewer_SOURCE_DIR}/QDockUtilities.h
)


SET( QHDFViewer_MOC_HDRS
  ${QHDFViewer_SOURCE_DIR}/QHDFViewerApplication.h
  ${QHDFViewer_SOURCE_DIR}/QHDFViewer.h
  ${QHDFViewer_SOURCE_DIR}/QHDFDataTableWindow.h
  ${QHDFViewer_SOURCE_DIR}/QHDFStringDataWindow.h
  ${QHDFViewer_SOURCE_DIR}/QHDFImageDataWindow.h
  ${QHDFViewer_SOURCE_DIR}/QHDFGroupPropertiesWindow.h
  ${QHDFViewer_SOURCE_DIR}/QHDFPropertiesWindow.h
  ${QHDFViewer_SOURCE_DIR}/QHDF5TableModel.h
  ${QHDFViewer_SOURCE_DIR}/IQHDF5Dataset.h
  ${QHDFViewer_SOURCE_DIR}/QHDF5TreeModel.h
  ${QHDFViewer_SOURCE_DIR}/IH5DataWindow.h
  ${QHDFViewer_SOURCE_DIR}/HDFGraphicsDelegate.h
  ${QHDFViewer_SOURCE_DIR}/IQPixmapGenerator.h
  ${QHDFViewer_SOURCE_DIR}/QRecentFileList.h
  ${QHDFViewer_SOURCE_DIR}/ImageDataToolbar.h
)
  
cmp_IDE_SOURCE_PROPERTIES( "QHDFViewer" "${QHDFViewer_HDRS};${QHDFViewer_MOC_HDRS}" "${QHDFViewer_SRCS}" "${PROJECT_INSTALL_HEADERS}")



# The UI files (As generated by Qt Designer
SET( QHDFViewer_UIS
  ${QHDFViewer_SOURCE_DIR}/UI_Files/QHDFViewer.ui
  ${QHDFViewer_SOURCE_DIR}/UI_Files/QHDFDataTableWindow.ui
  ${QHDFViewer_SOURCE_DIR}/UI_Files/QHDFStringDataWindow.ui
  ${QHDFViewer_SOURCE_DIR}/UI_Files/QHDFImageDataWindow.ui
  ${QHDFViewer_SOURCE_DIR}/UI_Files/QHDFGroupPropertiesWindow.ui
  ${QHDFViewer_SOURCE_DIR}/UI_Files/QHDFPropertiesWindow.ui
  )
cmp_IDE_GENERATED_PROPERTIES("QHDFViewer/UI_Files" "${QHDFViewer_UIS}" "")

# and finally any resource files
#SET(QHDFViewer_RCS ${QHDFViewer_SOURCE_DIR}/Templates.qrc)

# -- Run MOC and UIC on the necessary files
QT4_ADD_RESOURCES( QHDFViewer_Generated_RC_SRCS ${QHDFViewer_RCS} ${QRC_LICENSE_FILES})

# this will run uic on .ui files:
QT4_WRAP_UI( QHDFViewer_Generated_UI_HDRS ${QHDFViewer_UIS} )

# and finally this will run moc:
QT4_WRAP_CPP( QHDFViewer_Generated_MOC_SRCS ${QHDFViewer_MOC_HDRS} )

#-- Put the generated files into their own group for IDEs
cmp_IDE_GENERATED_PROPERTIES("Generated/ui" "${QHDFViewer_Generated_UI_HDRS}" "")
cmp_IDE_GENERATED_PROPERTIES("Generated/moc" "${QHDFViewer_Generated_MOC_SRCS}" "")
cmp_IDE_GENERATED_PROPERTIES("Generated/qrc" "${QRC_LICENSE_FILES}" "${QHDFViewer_Generated_RC_SRCS}")

# -- Finally set the Project Sources to include all the sources
set(${PROJECT_NAME}_PROJECT_SRCS
    ${QHDFViewer_SRCS}
    ${QHDFViewer_HDRS}
    ${QHDFViewer_MOC_HDRS}
    ${QHDFViewer_Generated_RC_SRCS}
    ${QHDFViewer_Generated_UI_HDRS}
    ${QHDFViewer_Generated_MOC_SRCS}
  )

#message(STATUS "${PROJECT_NAME}_PROJECT_SRCS: ${${PROJECT_NAME}_PROJECT_SRCS}")
include_directories(${QHDFViewer_SOURCE_DIR} )
include_directories(${QHDFViewer_SOURCE_DIR}/.. )
include_directories(${QHDFViewer_BINARY_DIR} )
include_directories(${DREAM3DProj_BINARY_DIR}/QtSupport)
include_directories(${DREAM3DProj_SOURCE_DIR}/Source/QtSupport)



if(APPLE)
    set(APPLICATION_ICON ${PROJECT_RESOURCES_DIR}/icons/icns/${PROJECT_NAME}.icns)
endif(APPLE)

if(Q_WS_WIN)
    set(APPLICATION_ICON ${PROJECT_RESOURCES_DIR}/icons/ico/${PROJECT_NAME}.ico)
ENDif(Q_WS_WIN)

set(DEST_DIR ".")
if(UNIX AND NOT APPLE)
    set(DEST_DIR "bin")
endif()

BuildQtAppBundle(
    TARGET ${PROJECT_NAME}
    SOURCES ${QHDFViewer_PROJECT_SRCS}
    DEBUG_EXTENSION ${EXE_DEBUG_EXTENSION}
    ICON_FILE  ${APPLICATION_ICON}
    VERSION_MAJOR ${DREAM3D_VER_MAJOR}
    VERSION_MINOR ${DREAM3D_VER_MINOR}
    VERSION_PATCH ${DREAM3D_VER_PATCH}
    BINARY_DIR    ${${PROJECT_NAME}_BINARY_DIR}
    LINK_LIBRARIES ${QT_LIBRARIES} QtSupport
    LIB_SEARCH_DIRS ${CMAKE_LIBRARY_OUTPUT_DIRECTORY} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
#    QT_PLUGINS    ${QT_PLUGINS}
#    OTHER_PLUGINS ${OTHER_PLUGINS}
    COMPONENT     Applications
    INSTALL_DEST  ${DEST_DIR}
)

# This section creates a custom info.plist for the .app bundle to allow for 
# custom document icons to be used. We configure the plist and also set the
# source file properties for the document icon.
if (APPLE)
    set_target_properties(${PROJECT_NAME} PROPERTIES 
             MACOSX_BUNDLE_INFO_PLIST ${QHDFViewer_SOURCE_DIR}/QHDFViewer.plist.in)                     
endif()



