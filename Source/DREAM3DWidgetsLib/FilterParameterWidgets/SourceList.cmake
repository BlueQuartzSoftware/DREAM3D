
set(DREAM3DWidgetsLib_ParameterWidgets_HDRS "")
set(DREAM3DWidgetsLib_ParameterWidgets_SRCS "")
set(DREAM3DWidgetsLib_ParameterWidgets_UIS "")


set(DREAM3D_PARAMETER_WIDGETS
    AttributeMatrixCreationWidget
    AttributeMatrixSelectionWidget
    AxisAngleWidget
    BooleanWidget
    ChoiceWidget
    ComparisonSelectionWidget
    DataArrayCreationWidget
    DataArraySelectionWidget
    DataBundleSelectionWidget
    DataContainerArrayProxyWidget
    DataContainerReaderWidget
    DataContainerSelectionWidget
    DoubleWidget
    DynamicChoiceWidget
    DynamicTableWidget
    FileListInfoWidget
    FloatVec3Widget
    FloatVec4Widget
    FourthOrderPolynomialWidget
    InputFileWidget
    InputPathWidget
    IntVec3Widget
    IntWidget
    LinkedBooleanWidget
    MultiDataArraySelectionWidget
    OutputFileWidget
    OutputPathWidget
    PreflightUpdatedValueWidget
    SecondOrderPolynomialWidget
    SeparatorWidget
    StringWidget
    Symmetric6x6Widget
    ThirdOrderPolynomialWidget
)

set(DREAM3D_PARAMETER_WIDGETS_NO_CODEGEN
      PhaseTypeSelectionWidget
      ShapeTypeSelectionWidget
      UnknownWidget
      VolumeDataContainerInfoWidget
)

set(REGISTER_KNOWN_WIDGETS ${DREAM3DWidgetsLib_BINARY_DIR}/FilterWidgetManager_RegisterWidgets_tmp.cpp)
set(FILTER_PARAMETER_CODEGEN_FILE ${DREAM3DWidgetsLib_BINARY_DIR}/Filter_Parameter_CodeGenFactory.cpp)
set(FILTER_PARAMETER_CODEGEN_INCLUDES_FILE ${DREAM3DWidgetsLib_BINARY_DIR}/Filter_Parameter_CodeGenFactory_Includes.cpp)
set(FILTER_PARAMETER_TYPELIST_FILE ${DREAM3DWidgetsLib_BINARY_DIR}/Filter_Parameter_TypeList.cpp)
set(FILTER_WIDGET_HEADERS ${DREAM3DWidgetsLib_BINARY_DIR}/FilterWidgetHeaders_tmp.h)

file(WRITE ${REGISTER_KNOWN_WIDGETS}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")
file(WRITE ${FILTER_PARAMETER_CODEGEN_FILE}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")
file(WRITE ${FILTER_PARAMETER_CODEGEN_INCLUDES_FILE}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")
file(WRITE ${FILTER_PARAMETER_TYPELIST_FILE}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")
file(WRITE ${FILTER_WIDGET_HEADERS}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")

file(APPEND  ${FILTER_PARAMETER_TYPELIST_FILE} "QList<QString> list;\n")

set(loopCounter 1)
foreach(FPW ${DREAM3D_PARAMETER_WIDGETS})
  set(DREAM3DWidgetsLib_ParameterWidgets_HDRS ${DREAM3DWidgetsLib_ParameterWidgets_HDRS}
    ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/${FPW}.h
    )
  set(DREAM3DWidgetsLib_ParameterWidgets_SRCS ${DREAM3DWidgetsLib_ParameterWidgets_SRCS}
    ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/${FPW}.cpp
    )
  set(DREAM3DWidgetsLib_ParameterWidgets_UIS ${DREAM3DWidgetsLib_ParameterWidgets_UIS}
    ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/UI_Files/${FPW}.ui
    )

  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "{\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "  PipelineFilterWidgetFactory<${FPW}>::Pointer factory = PipelineFilterWidgetFactory<${FPW}>::New();\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "  idManager->addFilterWidgetFactory( \"${FPW}\", factory );\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "}\n")
  file(APPEND  ${FILTER_WIDGET_HEADERS} "#include \"DREAM3DWidgetsLib/FilterParameterWidgets/${FPW}.h\"\n")

  file(APPEND  ${FILTER_PARAMETER_CODEGEN_INCLUDES_FILE} "#include \"DevHelper/CodeGenerators/${FPW}CodeGenerator.h\"\n")

  file(APPEND  ${FILTER_PARAMETER_TYPELIST_FILE} "list.push_back(\"${FPW}\");\n")

  if (loopCounter EQUAL 1)
    file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "if (fpType == \"${FPW}\") {\n")
  else()
    file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "else if (fpType == \"${FPW}\") {\n")
  endif()

  file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "  ${FPW}CodeGenerator::Pointer ptr = ${FPW}CodeGenerator::New(humanLabel, propertyName, fpCategory, initValue);\n")
      file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "  return ptr;\n}\n\n")

  set(loopCounter loopCounter+1)
endforeach()

foreach(FPW ${DREAM3D_PARAMETER_WIDGETS_NO_CODEGEN})
  set(DREAM3DWidgetsLib_ParameterWidgets_HDRS ${DREAM3DWidgetsLib_ParameterWidgets_HDRS}
    ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/${FPW}.h
    )
  set(DREAM3DWidgetsLib_ParameterWidgets_SRCS ${DREAM3DWidgetsLib_ParameterWidgets_SRCS}
    ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/${FPW}.cpp
    )
  set(DREAM3DWidgetsLib_ParameterWidgets_UIS ${DREAM3DWidgetsLib_ParameterWidgets_UIS}
    ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/UI_Files/${FPW}.ui
    )

  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "{\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "  PipelineFilterWidgetFactory<${FPW}>::Pointer factory = PipelineFilterWidgetFactory<${FPW}>::New();\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "  idManager->addFilterWidgetFactory( \"${FPW}\", factory );\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "}\n")
  file(APPEND  ${FILTER_WIDGET_HEADERS} "#include \"DREAM3DWidgetsLib/FilterParameterWidgets/${FPW}.h\"\n")
endforeach()

file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "else {\n")
file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "  FPCodeGenerator::Pointer ptr = FPCodeGenerator::New(humanLabel, propertyName, fpCategory, initValue);\n")
file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "  return ptr;\n}\n\n")

file(APPEND  ${FILTER_PARAMETER_TYPELIST_FILE} "qSort(list.begin(), list.end());\nreturn list;\n")

cmpReplaceFileIfDifferent(OLD_FILE_PATH ${DREAM3DWidgetsLib_BINARY_DIR}/FilterWidgetManager_RegisterWidgets.cpp
                          NEW_FILE_PATH ${DREAM3DWidgetsLib_BINARY_DIR}/FilterWidgetManager_RegisterWidgets_tmp.cpp)
cmpReplaceFileIfDifferent(OLD_FILE_PATH ${DREAM3DWidgetsLib_BINARY_DIR}/FilterWidgetHeaders.h
                          NEW_FILE_PATH ${DREAM3DWidgetsLib_BINARY_DIR}/FilterWidgetHeaders_tmp.h)


# --------------------------------------------------------------------
# Add some support files that still need MOC and UIC to be run on them
set(DREAM3DWidgetsLib_ParameterWidgets_HDRS
  ${DREAM3DWidgetsLib_ParameterWidgets_HDRS}
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidget.h
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/ComparisonSelectionTableModel.h
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/ComparisonSelectionItemDelegate.h
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/DynamicTableItemDelegate.h
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidgetsDialogs.h
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidgetUtils.hpp
)
set(DREAM3DWidgetsLib_ParameterWidgets_SRCS
  ${DREAM3DWidgetsLib_ParameterWidgets_SRCS}
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidget.cpp
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/ComparisonSelectionTableModel.cpp
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/ComparisonSelectionItemDelegate.cpp
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/DynamicTableItemDelegate.cpp
  ${DREAM3DWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidgetsDialogs.cpp
)
set(DREAM3DWidgetsLib_ParameterWidgets_UIS
  ${DREAM3DWidgetsLib_ParameterWidgets_UIS}
)




cmp_IDE_SOURCE_PROPERTIES( "DREAM3DWidgetsLib/FilterParameterWidgets" "${DREAM3DWidgetsLib_ParameterWidgets_HDRS}" "${DREAM3DWidgetsLib_ParameterWidgets_SRCS}" "0")

cmp_IDE_GENERATED_PROPERTIES("DREAM3DWidgetsLib/FilterParameterWidgets/UI_Files" "${DREAM3DWidgetsLib_ParameterWidgets_UIS}" "")

# --------------------------------------------------------------------
# and finally this will run moc:
#QT5_WRAP_CPP( DREAM3DWidgetsLib_ParameterWidgets_Generated_MOC_SRCS ${DREAM3DWidgetsLib_ParameterWidgets_HDRS} )

# These generated moc files will be #include in the FilterWidget source file that
# are generated so we need to tell the build system to NOT compile these files
set_source_files_properties( ${DREAM3DWidgetsLib_ParameterWidgets_Generated_MOC_SRCS} PROPERTIES HEADER_FILE_ONLY TRUE)

# -- Run MOC and UIC on the necessary files
# QT5_ADD_RESOURCES( DREAM3DWidgetsLib_Generated_RC_SRCS "${DREAM3DProj_SOURCE_DIR}/Documentation/Filters/Generated_FilterDocs.qrc"  )

# --------------------------------------------------------------------
# Continue on with our Qt4 section
QT5_WRAP_UI( DREAM3DWidgetsLib_ParameterWidgets_Generated_UI_HDRS ${DREAM3DWidgetsLib_ParameterWidgets_UIS} )

# --------------------------------------------------------------------
#-- Put the Qt generated files into their own group for IDEs
cmp_IDE_SOURCE_PROPERTIES( "Generated/Qt_Moc" "" "${DREAM3DWidgetsLib_ParameterWidgets_Generated_MOC_SRCS}" "0")
cmp_IDE_SOURCE_PROPERTIES( "Generated/Qt_Uic" "${DREAM3DWidgetsLib_ParameterWidgets_Generated_UI_HDRS}" "" "0")
#cmp_IDE_SOURCE_PROPERTIES( "Generated/Qt_Qrc" "${DREAM3DWidgetsLib_Generated_RC_SRCS}" "" "0")




