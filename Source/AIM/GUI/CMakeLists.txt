# set project's name
PROJECT( AIMRepresentation )
cmake_minimum_required(VERSION 2.6)


# ---------- Setup output Directories -------------------------
SET (CMAKE_LIBRARY_OUTPUT_DIRECTORY
  ${PROJECT_BINARY_DIR}/Bin
  CACHE PATH
  "Single Directory for all Libraries"
  )

# --------- Setup the Executable output Directory -------------
SET (CMAKE_RUNTIME_OUTPUT_DIRECTORY
  ${PROJECT_BINARY_DIR}/Bin
  CACHE PATH
  "Single Directory for all Executables."
  )

# --------- Setup the Executable output Directory -------------
SET (CMAKE_ARCHIVE_OUTPUT_DIRECTORY
  ${PROJECT_BINARY_DIR}/Bin
  CACHE PATH
  "Single Directory for all static libraries."
  )

add_definitions(-DAIM_USE_QT)

# Set the GUI type for different platforms
SET(GUI_TYPE )

# Set some Win32 Specific Settings
IF(WIN32)
 SET(GUI_TYPE WIN32)
ENDIF(WIN32)


# List the source files here
SET (AIMRepresentation_SRCS
    ${AIMRepresentation_SOURCE_DIR}/main.cpp
    ${AIMRepresentation_SOURCE_DIR}/AIMRepresentation.cpp
)

SET (AIMRepresentation_HDRS
    ${AIMRepresentation_SOURCE_DIR}/AIMRepresentation.h
)
IDE_SOURCE_PROPERTIES( "Source/AIM/Representation/GUI" "${AIMRepresentation_HDRS}" "${AIMRepresentation_SRCS}")


set (AIM_COMMON_QT_SRCS
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Common/Qt/AIMAboutBox.cpp
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Common/Qt/QRecentFilelist.cpp   )
set (AIM_COMMON_QT_HDRS
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Common/Qt/AIMAboutBox.h
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Common/Qt/QRecentFilelist.h  )
IDE_SOURCE_PROPERTIES("Source/AIM/Common/Qt" "${AIM_COMMON_QT_HDRS}" "${AIM_COMMON_QT_SRCS}")

set (AIM_Threads_SRCS 
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Threads/AIMThread.cpp
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Threads/AIMThreadGroup.cpp
)                       
set (AIM_Threads_HDRS
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Threads/AIMThread.h
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Threads/AIMThreadGroup.h   )
IDE_SOURCE_PROPERTIES("Source/AIM/Threads" "${AIM_Threads_HDRS}" "${AIM_Threads_SRCS}")


set (AIM_Reconstruction_SRCS 
    ${Reconstruction_SOURCE_DIR}/Reconstruction.cpp
)                       
set (AIM_Reconstruction_HDRS
    ${Reconstruction_SOURCE_DIR}/Reconstruction.h
 )
IDE_SOURCE_PROPERTIES("Source/AIM/Representation/Reconstruction" "${AIM_Reconstruction_HDRS}" "${AIM_Reconstruction_SRCS}")


SET( AIMRepresentation_MOC_HDRS
    ${AIMRepresentation_SOURCE_DIR}/AIMRepresentation.h
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Common/Qt/AIMAboutBox.h
    ${AIMBlades_SOURCE_DIR}/Source/AIM/Common/Qt/QRecentFilelist.h
    ${Reconstruction_SOURCE_DIR}/Reconstruction.h
    ${AIM_Threads_HDRS}
)

# The UI files As generated by Qt Designer
SET( AIMRepresentation_UIS
  ${AIMRepresentation_SOURCE_DIR}/UI_Files/AIMRepresentation.ui
  ${AIMBlades_SOURCE_DIR}/Source/AIM/Common/Qt/UI_Files/AIMAboutBox.ui
)

# and finally any resource files
SET( AIMRepresentation_RCS ${AIMBlades_SOURCE_DIR}/Resources/icons/images/Icons.qrc)

# -- Run MOC and UIC on the necessary files   
QT4_ADD_RESOURCES( Generated_RC_SRCS ${AIMRepresentation_RCS} )

# this will run uic on .ui files:
QT4_WRAP_UI( Generated_UI_HDRS ${AIMRepresentation_UIS} )

# and finally this will run moc:
QT4_WRAP_CPP( Generated_MOC_SRCS ${AIMRepresentation_MOC_HDRS} )

#-- Put the generated files into their own group for IDEs
IDE_SOURCE_PROPERTIES( "Generated" "${Generated_UI_HDRS}" "${Generated_MOC_SRCS}")

# -- Finally set the Project Sources to include all the sources
SET (AIMRepresentation_PROJECT_SRCS 
    ${AIMRepresentation_SRCS}
    ${AIMRepresentation_HDRS}
    ${AIM_COMMON_QT_HDRS}
    ${AIM_COMMON_QT_SRCS}
    ${AIM_Threads_SRCS}
    ${AIM_Threads_HDRS}
    ${AIM_Reconstruction_SRCS}
    ${AIM_Reconstruction_HDRS}
    ${Generated_RC_SRCS} 
    ${Generated_UI_HDRS} 
    ${Generated_MOC_SRCS}
)


include_directories( ${AIMRepresentation_BINARY_DIR} )
#-- Set up OS X Application Bundle properties
#-- This file have tools for OS X Bunlde creation and configuration
INCLUDE (${AIMBlades_SOURCE_DIR}/Resources/OSX_Tools/OSX_BundleTools.cmake)
SET (EXE_DEBUG_EXTENSION "_debug")
IF(APPLE)
 SET(GUI_TYPE MACOSX_BUNDLE)
 ConfigureMacOSXBundlePlist(${PROJECT_NAME} ${EXE_DEBUG_EXTENSION} 
                            ${AIMBlades_RESOURCES_DIR}/icons/icns/${PROJECT_NAME}.icns
                            ${AIMBlades_VERSION} )
ENDIF (APPLE)

IF(Q_WS_WIN)
    include_directories(${AIMBlades_RESOURCES_DIR}/icons/ico)
    configure_file(${AIMBlades_RESOURCES_DIR}/icons/ico/IconResource.rc.in ${${PROJECT_NAME}_BINARY_DIR}/${PROJECT_NAME}.rc)
    SET (${PROJECT_NAME}_PROJECT_SRCS ${${PROJECT_NAME}_PROJECT_SRCS} ${${PROJECT_NAME}_BINARY_DIR}/${PROJECT_NAME}.rc) 
    IDE_SOURCE_PROPERTIES ("ConfiguredFiles" "${${PROJECT_NAME}_BINARY_DIR}/${PROJECT_NAME}.rc" "")
ENDIF(Q_WS_WIN)

IDE_SOURCE_PROPERTIES ("ConfiguredFiles" "${AIMBlades_BINARY_DIR}/MXA/MXAConfiguration.h" "")
IDE_SOURCE_PROPERTIES ("ConfiguredFiles" "${AIMBlades_BINARY_DIR}/AIM/Common/AIMVersion.h" "")  

# ---------- Build the Viewer Application  ----------------------
ADD_EXECUTABLE( ${PROJECT_NAME} ${GUI_TYPE} ${AIMRepresentation_PROJECT_SRCS} )
TARGET_LINK_LIBRARIES( ${PROJECT_NAME}
                        RepresentationLibrary 
                        ${QT_LIBRARIES} 
                        ${ITK_LIBRARIES} 
                        ${TIFF_LIBRARIES} )

InstallationSupport(${PROJECT_NAME} ${EXE_DEBUG_EXTENSION} ${AIMRepresentation_BINARY_DIR})






